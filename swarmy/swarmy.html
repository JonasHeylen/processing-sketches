<html>
<head>
<title>Swarmy</title>
<script language="javascript" src="processing-0.9.7.min.js"></script>
<script type="text/javascript">
  if(window.addEventListener){window.addEventListener("load",function(){var scripts=document.getElementsByTagName("script");for(var i=0;i<scripts.length;i++){if(scripts[i].type=="application/processing"){var src=scripts[i].src,canvas=scripts[i].nextSibling;if(src&&src.indexOf("#")){canvas=document.getElementById(src.substr(src.indexOf("#")+1));}else{while(canvas&&canvas.nodeName.toUpperCase()!="CANVAS")canvas=canvas.nextSibling;}if(canvas){Processing(canvas,scripts[i].text);}}}},false);}
</script>
<script type="text/javascript">

	var _gaq = _gaq || [];
	_gaq.push(['_setAccount', 'UA-19548543-1']);
	_gaq.push(['_trackPageview']);

	(function() {
	 var ga = document.createElement('script'); ga.type = 'text/javascript';
	 ga.async = true;
	 ga.src = ('https:' == document.location.protocol ? 'https://ssl' :
		 'http://www') + '.google-analytics.com/ga.js';
	 var s = document.getElementsByTagName('script')[0];
	 s.parentNode.insertBefore(ga, s);
	 })();

</script>
</head>
<body>
<script type="application/processing">
/* CONSTANTS */

int SIZE = 512; // window size
int BG = 30;    // background color
int FG = 170;   // foreground color
int R = 5;      // radius
int N = 20;     // number of objects
double M = 1;   // mass
double K = 0.0001;     // spring constant
double F = 0.0002 * N; // coefficient of friction
double P = 0.5;        // random perturbation

/* STATE */

float[] posx = new float[N];
float[] posy = new float[N];
double[] dx = new double[N];
double[] dy = new double[N];

/* SETUP */

void setup() {
  smooth();
  frameRate(30);
  size(SIZE, SIZE);
  background(BG);
  fill(FG);
  noStroke();

  // random initial positions
  for(int i = 0; i < N; i++) {
    posx[i] = random(SIZE);
    posy[i] = random(SIZE);
    dx[i] = 0;
    dy[i] = 0;
  }
}

/* DRAW */

void draw() {
  background(BG);
  for(int i = 0; i < N; i++) {
    calculateDxy(i);
    calculatePos(i);
    fill(255-abs((float) dx[i])*20, 255-abs((float) dy[i])*20, 255);
    e(posx[i], posy[i]);
  }
}

/* PHYSICS */

void calculatePos(int i) {
  posx[i] += dx[i];
  posy[i] += dy[i];
}

void calculateDxy(int i) {
  double ddx = 0;
  double ddy = 0;
  for(int j = 0; j < N; j++) {
    if (i != j) {
      /* spring force */
      ddx += K * (posx[j] - posx[i]) / M;
      ddy += K * (posy[j] - posy[i]) / M;
    }
  }
  /* follow mouse */
  ddx += N * K * (mouseX - posx[i]) / M;
  ddy += N * K * (mouseY - posy[i]) / M;
  /* friction */
  ddx += - F * dx[i];
  ddy += - F * dy[i];
  /* random perturbations */
  ddx += random(-P, P);
  ddy += random(-P, P);

  dx[i] += ddx;
  dy[i] += ddy;
}

/* HELPERS */

void e(float x, float y) {
  ellipse(x, y, R, R);
}

</script><canvas width="512px" height="512px"></canvas>
<p>Swarmy by <a href="http://jonasheylen.posterous.com/">Jonas Heylen</a> (<a
  href="http://twitter.com/JonasHeylen">Twitter</a>)</p>
<p>Created with <a href="http://processingjs.org/">Processing.js</a></p>
</body>
</html>

